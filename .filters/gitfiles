#!/bin/sh
############################################################ IDENT(1)
#
# $Title: Script to convert git file paths on stdin to command argv elements $
#
############################################################ INFORMATION
#
# System requirements: awk(1) xargs(1)
#
############################################################ MAIN

awk '
	BEGIN {
		c = split("a\ab\bt\tn\nv\vf\fr\r\"\"", cmap, "")
		for (n = 1; n < c; n += 2) map["\\\\" cmap[n]] = cmap[n+1]
	}
	function o2d(oct) {
		dec = 0
		while ((o = substr(oct, 1, 1)) ~ /[0-7]/) {
			dec = (dec + o) * 8
			oct = substr(oct, 2)
		}
		return dec / 8
	}
	{
		if (/^".*"$/) $0 = substr($0, 2, length($0) - 2)
		for (m in map) gsub(m, map[m]); gsub(/\\\\/, "\\")
		while (match($0, /\\[0-7][0-7][0-7]/))
			$0 = sprintf("%s%c%s", substr($0, 1, RSTART - 1),
				o2d(substr($0, RSTART + 1, RLENGTH - 1)),
				substr($0, RSTART + RLENGTH))
		printf "%s%c", $0, 0
	}
' | xargs -r0 -- "$@"

################################################################################
# END
################################################################################
#
# $Copyright: 2015 The FrauBSD Project. All rights reserved. $
#
# $FrauBSD: .filters/gitfiles 2015-10-21 07:24:19 -0700 freebsdfrau $
#
################################################################################
